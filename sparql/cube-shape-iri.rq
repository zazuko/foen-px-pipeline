BASE <http://environment.ld.admin.ch/foen/>
PREFIX cube: <http://ns.bergnet.org/cube/>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX ex: <http://example.org/>

INSERT {
  ?cubeShape a sh:NodeShape, cube:ConstraintShape ;
    sh:closed true ;
    sh:property [
      sh:path ?property ;
      ex:in ?valueString ;
      sh:nodeKind sh:IRI 
    ]
} WHERE {

  # Apparently I do need two SELECTs, otherwise it does not what I expect. Someone needs to explain me why...
  SELECT DISTINCT ?cubeShape ?property (GROUP_CONCAT( STR(?value); separator=' ') AS ?valueString )  WHERE {
    SELECT DISTINCT ?cubeShape ?property ?value WHERE {
      ?cube  a cube:Cube ;
        cube:observations/cube:observation ?observation ;
        cube:observationShape ?cubeShape .

      ?observation ?property ?value .

      FILTER(isIRI(?value))
      FILTER(?property NOT IN (qb:dataSet, rdf:type ))

    } GROUP BY ?cubeShape ?property ?value ORDER BY ?value
      
  }GROUP BY ?cubeShape ?property
}  